allprojects {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_11

    dependencies {
        implementation 'io.projectreactor:reactor-core'
        implementation 'io.projectreactor.addons:reactor-extra'

        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        compileOnly 'org.projectlombok:lombok:1.18.16'
        annotationProcessor 'org.projectlombok:lombok:1.18.16'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
        testCompileOnly 'org.projectlombok:lombok:1.18.16'
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }

    test.finalizedBy(project.tasks.jacocoTestReport)

    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            xml.destination file("${buildDir}/reports/jacoco.xml")
            csv.enabled false
            html.destination file("${buildDir}/reports/jacocoHtml")
        }
    }

    ext.libs = [
            spring_boot   : [
                    "org.springframework.boot:spring-boot-starter-actuator",
                    "org.springframework.boot:spring-boot-starter-logging:${springBootVersion}",
                    "org.springframework.boot:spring-boot-devtools:${springBootVersion}",
                    "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"
            ],
            jack          : [
                    "com.fasterxml.jackson.core:jackson-annotations:2.10.0"
            ],
            api_validation: [
                    "javax.validation:validation-api:2.0.1.Final"
            ],
            config        : [
                    "org.springframework.cloud:spring-cloud-starter-config",
                    "org.springframework.retry:spring-retry"
            ],
            unit_tests: [
                    "junit:junit:4.+",
                    "org.mockito:mockito-core:2.23.0",
                    "com.squareup.okhttp3:okhttp:4.2.2",
                    "com.squareup.okhttp3:mockwebserver:4.2.2",
                    "org.assertj:assertj-core:3.3.0",
                    "io.projectreactor:reactor-test:3.3.0.RELEASE",
                    "org.springframework.boot:spring-boot-starter-test:${springBootVersion}",
                    "org.powermock:powermock-api-mockito2:2.0.0-beta.5",
                    "org.powermock:powermock-module-junit4:2.0.0-beta.5"
            ]
    ]

}

jacoco {
    toolVersion = "${jacocoVersion}"
    reportsDir = file("$buildDir/reports")
}

task jacocoMergedReport(type: JacocoReport) {
    dependsOn = subprojects.jacocoTestReport
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}
